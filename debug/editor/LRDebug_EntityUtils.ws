/**
 * Entity and component utilities for the LRDebug light editing system.
 *
 * - Free functions for component/rewriter access
 * - @addField and @addMethod extensions on CGameplayEntity and ILightSourceRewriter
 * - @wrapMethod hooks that track inOriginalState on ILightSourceRewriter
 */

// ---- Component helpers ----

@addField(CGameplayEntity) public var lrDebugLightCountsCached: bool;
@addField(CGameplayEntity) public var lrDebugPointLightCount: int;
@addField(CGameplayEntity) public var lrDebugSpotLightCount: int;

@addMethod(CGameplayEntity)
public function LRDebug_CacheLightCounts() {
    if (lrDebugLightCountsCached) return;

    lrDebugPointLightCount = GetComponentsCountByClassName('CPointLightComponent');
    lrDebugSpotLightCount = GetComponentsCountByClassName('CSpotLightComponent');
    lrDebugLightCountsCached = true;
}

@addMethod(CGameplayEntity)
public function LRDebug_PointLightCount(): int {
    LRDebug_CacheLightCounts();
    return lrDebugPointLightCount;
}

@addMethod(CGameplayEntity)
public function LRDebug_SpotLightCount(): int {
    LRDebug_CacheLightCounts();
    return lrDebugSpotLightCount;
}

@addMethod(CGameplayEntity)
public function HasPointLight(): bool {
    return LRDebug_PointLightCount() > 0;
}

@addMethod(CGameplayEntity)
public function HasSpotLight(): bool {
    return LRDebug_SpotLightCount() > 0;
}

function LRDebug_PointLightAt(entity: CGameplayEntity, index: int): CPointLightComponent {
    var components: array<CComponent>;
    components = entity.GetComponentsByClassName('CPointLightComponent');
    if (index >= 0 && index < components.Size()) return (CPointLightComponent)components[index];
    return NULL;
}

function LRDebug_SpotLightAt(entity: CGameplayEntity, index: int): CSpotLightComponent {
    var components: array<CComponent>;
    components = entity.GetComponentsByClassName('CSpotLightComponent');
    if (index >= 0 && index < components.Size()) return (CSpotLightComponent)components[index];
    return NULL;
}

// ---- Entity classification ----

function LRDebug_IsCandle(entity: CGameplayEntity): bool {
    return StrFindFirst(entity.ToString(), "candle") != -1 &&
        StrFindFirst(entity.ToString(), "candle_holder") == -1;
}

function LRDebug_GuessRewriterType(entity: CGameplayEntity): ELightRewriteType {
    if (LRDebug_IsCandle(entity)) return LRT_Candle;
    return LRT_Unknown;
}

// ---- CGameplayEntity extensions ----

/** The params used to edit the light source */
@addField(CGameplayEntity) public var lrDebugParams: CLightRewriteSourceParams;

/** Pre-edit snapshot the export diffs against, so profile-inherited values aren't re-emitted */
@addField(CGameplayEntity) public var lrDebugBaseline: CLightRewriteSourceParams;

/** Lazy getter. Copies current effective params on first call, keeping a baseline for the export */
@addMethod(CGameplayEntity)
public function LRDebug_GetParams(rewriter: ILightSourceRewriter): CLightRewriteSourceParams {
    var effective: CLightRewriteSourceParams;

    if (!lrDebugParams) {
        lrDebugParams = new CLightRewriteSourceParams in this;
        lrDebugBaseline = new CLightRewriteSourceParams in this;

        effective = rewriter.LRDebug_GetEffectiveParams();
        effective.ApplyTo(lrDebugParams);
        effective.ApplyTo(lrDebugBaseline);

        // A profile with no spotlight still needs a baseline to diff a mid-session spotlight against
        if (!lrDebugBaseline.spotlight) {
            lrDebugBaseline.spotlight = new CLightRewriteSpotlightParams in this;
        }

        lrDebugParams.enabled.has = true;
        lrDebugParams.enabled.value = true;
    }
    return lrDebugParams;
}

@addMethod(CGameplayEntity)
public function LRDebug_ClearDebugParams() {
    lrDebugParams = NULL;
    lrDebugBaseline = NULL;
}

// ---- ILightSourceRewriter extensions ----

/** Whether the rewriter is in its original state */
@addField(ILightSourceRewriter) public var inOriginalState: bool;

@wrapMethod(CCandleLightRewriter)
function RewriteLight() {
    wrappedMethod();
    inOriginalState = false;
}

@wrapMethod(CGenericLightRewriter)
function RewriteLight() {
    wrappedMethod();
    inOriginalState = false;
}

@wrapMethod(CSpotlightLightRewriter)
function RewriteLight() {
    wrappedMethod();
    inOriginalState = false;
}

@wrapMethod(ILightSourceRewriter)
function RestoreOriginalState() {
    wrappedMethod();
    inOriginalState = true;
}

@addMethod(ILightSourceRewriter)
public function LRDebug_GetEffectiveParams(): CLightRewriteSourceParams {
    return GetEffectiveParams();
}

// Override rewriter params

@addMethod(ILightSourceRewriter)
public function LRDebug_SetMenuOverrideParams(params: CLightRewriteSourceParams) {
    this.overrideParams = params;
}

@addMethod(ILightSourceRewriter)
public function LRDebug_ClearMenuOverrideParams() {
    this.overrideParams = NULL;
}

// ---- Rewriter access ----

/**
 * This is a slight clobbering of the Light Rewrite logic, that ensures the debug system works
 * correctly. Any entities not covered by the active Light Rewrite profile will be given a
 * debug rewriter.
 */
@addMethod(CGameplayEntity)
public function LRDebug_GetOrCreateRewriter(): ILightSourceRewriter {
    var params: CLightRewriteSourceParams;

    if (lightSourceRewriter) return lightSourceRewriter;

    params = theGame.GetLightRewriteSettings().FindParamsForEntity(this);
    if (!params) {
        params = new CLightRewriteSourceParams in this;
        params.enabled.has = true;
        params.enabled.value = true;
        params.rewriterType.has = true;
        params.rewriterType.value = LRDebug_GuessRewriterType(this);
        params.tag = 'LR_DebugLight';
        params.displayName = "debug";
    }

    bypassLightRewrite = false;
    lightSourceRewriter = theGame.lightRewrite.CreateRewriterFromParams(params, this);
    return lightSourceRewriter;
}
