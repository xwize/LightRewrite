/**
 * Owns the currently-selected attribute index and all per-attribute edit logic.
 *
 * The attribute edit functions intentionally do not call LRDebug_RegenerateText
 * on the target's oneliner - that is the caller's responsibility after each
 * operation so the call site stays explicit.
 */
class LRDebug_AttributeEditor {
    private var attrIndex        : int;
    private var adjustAccumulator: float;
    private var selectedLightType: name;  default selectedLightType = 'point';
    private var pointLightIndex: int;
    private var spotLightIndex : int;

    private var groupEdit: bool;  default groupEdit = true;
    private var groupEditTarget: CGameplayEntity;
    private var groupMembers   : array<CGameplayEntity>;

    private var history      : LRDebug_EditHistory;
    private var adjustChanged: bool;

    public function SetHistory(value: LRDebug_EditHistory) {
        history = value;
    }

    public function SetAttributeIndex(index: int) {
        attrIndex = index;
    }

    public function IsEditingOffset(): bool {
        return GetCurrentAttrId(selectedLightType) == 'alignOffsetZ';
    }

    public function IsEditingRadius(): bool {
        return GetCurrentAttrId(selectedLightType) == 'radius';
    }

    public function IsEditingSpotAngle(target: CGameplayEntity): bool {
        var attr: name = GetCurrentAttrId(GetSelectedLightType(target));
        return attr == 'innerAngle' || attr == 'outerAngle';
    }

    /** In spot mode slots 6/7/13 are the spotlight cone; every other slot is shared */
    public function GetCurrentAttrId(type: name): name {
        if (type == 'spot') {
            switch (attrIndex) {
                case 6:   return 'innerAngle';
                case 7:   return 'outerAngle';
                case 13:  return 'softness';
            }
        }

        switch (attrIndex) {
            case 0:   return 'brightness';
            case 1:   return 'radius';
            case 2:   return 'attenuation';
            case 3:   return 'shadowFadeDistance';
            case 4:   return 'shadowFadeRange';
            case 5:   return 'shadowBlendFactor';
            case 6:   return 'useSpotlightColor';
            case 7:   return 'alignPointLights';
            case 8:   return 'alignOffsetZ';
            case 9:   return 'overrideColour';
            case 10:  return 'colourR';
            case 11:  return 'colourG';
            case 12:  return 'colourB';
            case 13:  return 'softness';
        }
        return 'unknown';
    }

    public function GetCurrentAttrLabel(type: name): string {
        switch (GetCurrentAttrId(type)) {
            case 'brightness':          return "brightness";
            case 'radius':              return "radius";
            case 'attenuation':         return "attenuation";
            case 'shadowFadeDistance':  return "shadow distance";
            case 'shadowFadeRange':     return "shadow range";
            case 'shadowBlendFactor':   return "shadow blend";
            case 'useSpotlightColor':   return "use spotlight colour";
            case 'alignPointLights':    return "align point lights";
            case 'alignOffsetZ':        return "align offset Z";
            case 'innerAngle':          return "inner angle";
            case 'outerAngle':          return "outer angle";
            case 'softness':            return "softness";
            case 'overrideColour':      return "override colour";
            case 'colourR':             return "colour R";
            case 'colourG':             return "colour G";
            case 'colourB':             return "colour B";
        }
        return "unknown";
    }

    public function GetSelectedLightType(target: CGameplayEntity): name {
        if (target && target.HasSpotLight()) {
            if (selectedLightType == 'spot' || !target.HasPointLight()) return 'spot';
        }
        return 'point';
    }

    public function ResetLightIndices() {
        pointLightIndex = 0;
        spotLightIndex = 0;
    }

    /** Clamped to the live component count, so a stale index cannot outlive a despawned light */
    public function GetActiveLightIndex(target: CGameplayEntity, type: name): int {
        var count, index: int;

        if (type == 'spot') {
            count = target.LRDebug_SpotLightCount();
            index = spotLightIndex;
        }
        else {
            count = target.LRDebug_PointLightCount();
            index = pointLightIndex;
        }

        if (index >= count) return count - 1;
        return index;
    }

    public function CycleActiveLight(target: CGameplayEntity, delta: int): bool {
        var count: int;
        var type: name;

        if (!target) return false;

        type = GetSelectedLightType(target);
        if (type == 'spot') {
            count = target.LRDebug_SpotLightCount();
            if (count < 2) return false;

            spotLightIndex = (GetActiveLightIndex(target, type) + delta + count) % count;
        }
        else {
            count = target.LRDebug_PointLightCount();
            if (count < 2) return false;

            pointLightIndex = (GetActiveLightIndex(target, type) + delta + count) % count;
        }
        return true;
    }

    public function SwapLightSelection(target: CGameplayEntity) {
        var type: name;

        if (!target || !target.HasPointLight() || !target.HasSpotLight()) return;

        if (selectedLightType == 'spot') selectedLightType = 'point';
        else selectedLightType = 'spot';

        type = GetSelectedLightType(target);
        if (!IsAttrApplicable(GetCurrentAttrId(type), type)) {
            SetAttributeIndex(0);
        }
    }

    /** Whether an attribute can be edited on a given light type */
    private function IsAttrApplicable(attribute: name, lightType: name): bool {
        switch (attribute) {
            case 'innerAngle':
            case 'outerAngle':
            case 'softness':
                return lightType == 'spot';
            case 'useSpotlightColor':
            case 'alignPointLights':
                return lightType != 'spot';
        }
        return true;
    }

    public function GetActiveLight(target: CGameplayEntity): CLightComponent {
        if (!target) return NULL;
        return GetLight(target, GetSelectedLightType(target));
    }

    private function GetLight(target: CGameplayEntity, type: name): CLightComponent {
        if (type == 'spot') return LRDebug_SpotLightAt(target, GetActiveLightIndex(target, type));
        return LRDebug_PointLightAt(target, GetActiveLightIndex(target, type));
    }

    /** Edits always land in the per-component arrays; entity-wide fields stay an XML-authoring layer */
    private function GetActiveLightParams(
        params: CLightRewriteSourceParams,
        target: CGameplayEntity,
        type: name
    ): ILightRewriteParams {
        if (type == 'spot') return GetActiveSpotParams(params, target);
        return params.GetOrCreatePointLightParams(GetActiveLightIndex(target, type));
    }

    private function GetActiveSpotParams(
        params: CLightRewriteSourceParams,
        target: CGameplayEntity
    ): CLightRewriteSpotlightParams {
        return params.GetOrCreateSpotLightParams(GetActiveLightIndex(target, 'spot'));
    }

    /** Seed offset from the live position; it's absolute, so starting at 0 would teleport the light */
    private function SeedComponentOffset(params: ILightRewriteParams, light: CLightComponent) {
        if (params.offset.has) return;
        params.offset.has = true;
        if (light) params.offset.value = light.GetLocalPosition();
    }

    /** RoundF() is not used here because RoundF(0.05 * 100.0) / 100.0 == 0.04. */
    private function ClampAttributeValue(attr: name, value: float): float {
        var clamped: float;

        // alignOffsetZ is the only attribute that can go negative.
        switch (attr) {
            case 'brightness':          clamped = ClampF(value, 0.0, 400.0);   break;
            case 'radius':              clamped = ClampF(value, 0.0, 50.0);    break;
            case 'attenuation':         clamped = ClampF(value, 0.0, 1.0);     break;
            case 'shadowFadeDistance':  clamped = ClampF(value, 0.0, 100.0);   break;
            case 'shadowFadeRange':     clamped = ClampF(value, 0.0, 100.0);   break;
            case 'shadowBlendFactor':   clamped = ClampF(value, 0.0, 1.0);     break;
            case 'alignOffsetZ':        clamped = ClampF(value, -30.0, 30.0);  break;
            case 'innerAngle':          clamped = ClampF(value, 0.0, 360.0);   break;
            case 'outerAngle':          clamped = ClampF(value, 0.0, 360.0);   break;
            case 'softness':            clamped = ClampF(value, 0.0, 255.0);   break;
            default:                    return value;
        }

        if (clamped >= 0.0) return (float)FloorF(clamped * 100.0 + 0.5) / 100.0;
        return (float)CeilF(clamped * 100.0 - 0.5) / 100.0;
    }

    /** Analog hold-to-edit: adds a raw signed delta to the attribute and applies it */
    public function AdjustAttributeContinuous(
        delta: float,
        target: CGameplayEntity,
        optional attr: name
    ): bool {
        var spot: CSpotLightComponent;
        var light: CLightComponent;
        var params: CLightRewriteSourceParams;
        var lightParams: ILightRewriteParams;
        var spotParams: CLightRewriteSpotlightParams;
        var rewriter: ILightSourceRewriter;
        var type: name;

        if (delta == 0.0) return false;
        if (!target) return false;
        if (!target.lrdebugOneliner) return false;

        rewriter = target.LRDebug_GetOrCreateRewriter();
        params = target.LRDebug_GetParams(rewriter);
        spot = LRDebug_SpotLightAt(target, GetActiveLightIndex(target, 'spot'));

        type = GetSelectedLightType(target);
        if (attr == '') attr = GetCurrentAttrId(type);
        if (!IsAttrApplicable(attr, type)) return false;

        // Normalise per attribute so a full swipe covers each one's range (brightness feel).
        delta *= GetAxisScale(attr);

        // Release the accumulated movement in whole value-resolution steps
        delta = ConsumeQuantizedDelta(delta, GetAdjustQuantum(attr));
        if (delta == 0.0) return false;

        if (type == 'spot') light = spot;
        else light = LRDebug_PointLightAt(target, GetActiveLightIndex(target, type));

        switch (attr) {
            case 'brightness':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.brightness.has) {
                    lightParams.brightness.has = true;
                    if (light) lightParams.brightness.value = light.brightness;
                }
                lightParams.brightness.value = ClampAttributeValue(
                    attr,
                    lightParams.brightness.value + delta
                );
                break;

            case 'radius':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.radius.has) {
                    lightParams.radius.has = true;
                    if (light) lightParams.radius.value = light.radius;
                }
                lightParams.radius.value = ClampAttributeValue(
                    attr,
                    lightParams.radius.value + delta
                );
                break;

            case 'attenuation':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.attenuation.has) {
                    lightParams.attenuation.has = true;
                    if (light) lightParams.attenuation.value = light.attenuation;
                }
                lightParams.attenuation.value = ClampAttributeValue(
                    attr,
                    lightParams.attenuation.value + delta
                );
                break;

            case 'shadowFadeDistance':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.shadowFadeDistance.has) {
                    lightParams.shadowFadeDistance.has = true;
                    if (light) {
                        lightParams.shadowFadeDistance.value = light.shadowFadeDistance;
                    }
                }
                lightParams.shadowFadeDistance.value = ClampAttributeValue(
                    attr,
                    lightParams.shadowFadeDistance.value + delta
                );
                break;

            case 'shadowFadeRange':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.shadowFadeRange.has) {
                    lightParams.shadowFadeRange.has = true;
                    if (light) {
                        lightParams.shadowFadeRange.value = light.shadowFadeRange;
                    }
                }
                lightParams.shadowFadeRange.value = ClampAttributeValue(
                    attr,
                    lightParams.shadowFadeRange.value + delta
                );
                break;

            case 'shadowBlendFactor':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.shadowBlendFactor.has) {
                    lightParams.shadowBlendFactor.has = true;
                    if (light) {
                        lightParams.shadowBlendFactor.value = light.shadowBlendFactor;
                    }
                }
                lightParams.shadowBlendFactor.value = ClampAttributeValue(
                    attr,
                    lightParams.shadowBlendFactor.value + delta
                );
                break;

            case 'alignOffsetZ':
                if (type != 'spot' && LRDebug_IsCandle(target)) {
                    if (!params.alignPointLights.has) {
                        params.alignPointLights.has = true;
                        params.alignPointLights.value = true;
                    }
                    params.pointLightOffset.Z = ClampAttributeValue(
                        attr,
                        params.pointLightOffset.Z + delta
                    );
                }
                else {
                    lightParams = GetActiveLightParams(params, target, type);
                    SeedComponentOffset(lightParams, light);
                    lightParams.offset.value.Z = ClampAttributeValue(
                        attr,
                        lightParams.offset.value.Z + delta
                    );
                }
                break;

            case 'innerAngle':
                spotParams = GetActiveSpotParams(params, target);
                if (!spotParams.innerAngle.has) {
                    spotParams.innerAngle.has = true;
                    if (spot) spotParams.innerAngle.value = spot.innerAngle;
                }
                spotParams.innerAngle.value = ClampAttributeValue(
                    attr,
                    spotParams.innerAngle.value + delta
                );
                break;

            case 'outerAngle':
                spotParams = GetActiveSpotParams(params, target);
                if (!spotParams.outerAngle.has) {
                    spotParams.outerAngle.has = true;
                    if (spot) spotParams.outerAngle.value = spot.outerAngle;
                }
                spotParams.outerAngle.value = ClampAttributeValue(
                    attr,
                    spotParams.outerAngle.value + delta
                );
                break;

            case 'softness':
                spotParams = GetActiveSpotParams(params, target);
                if (!spotParams.softness.has) {
                    spotParams.softness.has = true;
                    if (spot) spotParams.softness.value = spot.softness;
                }
                spotParams.softness.value = ClampAttributeValue(
                    attr,
                    spotParams.softness.value + delta
                );
                break;

            case 'colourR':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.color.has) {
                    lightParams.color.has = true;
                    if (light) lightParams.color.value = light.color;
                }
                lightParams.color.value.Red = (byte)Clamp(lightParams.color.value.Red + (int)delta, 0, 255);
                break;

            case 'colourG':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.color.has) {
                    lightParams.color.has = true;
                    if (light) lightParams.color.value = light.color;
                }
                lightParams.color.value.Green = (byte)Clamp(lightParams.color.value.Green + (int)delta, 0, 255);
                break;

            case 'colourB':
                lightParams = GetActiveLightParams(params, target, type);
                if (!lightParams.color.has) {
                    lightParams.color.has = true;
                    if (light) lightParams.color.value = light.color;
                }
                lightParams.color.value.Blue = (byte)Clamp(lightParams.color.value.Blue + (int)delta, 0, 255);
                break;

            default:
                return false;
        }

        ApplyParams(target, rewriter, params);
        adjustChanged = true;
        return true;
    }

    /** Candles are excluded because their offset auto-aligns to FX slots and only its Z value can be exported */
    public function MoveOffsetXY(dx: float, dy: float, target: CGameplayEntity): bool {
        var params: CLightRewriteSourceParams;
        var lightParams: ILightRewriteParams;
        var rewriter: ILightSourceRewriter;
        var type: name;
        var scale: float;

        if (dx == 0.0 && dy == 0.0) return false;
        if (!target) return false;
        if (!target.lrdebugOneliner) return false;

        rewriter = target.LRDebug_GetOrCreateRewriter();
        params = target.LRDebug_GetParams(rewriter);

        type = GetSelectedLightType(target);
        if (type != 'spot' && LRDebug_IsCandle(target)) return false;

        // Normalise using the same value as Z-axis adjustment
        scale = GetAxisScale('alignOffsetZ');
        dx *= scale;
        dy *= scale;

        lightParams = GetActiveLightParams(params, target, type);
        SeedComponentOffset(lightParams, GetLight(target, type));
        lightParams.offset.value.X += dx;
        lightParams.offset.value.Y += dy;

        ApplyParams(target, rewriter, params);
        adjustChanged = true;
        return true;
    }

    /** Clears the offset override. */
    public function ResetOffset(target: CGameplayEntity): bool {
        var params: CLightRewriteSourceParams;
        var lightParams: ILightRewriteParams;
        var rewriter: ILightSourceRewriter;
        var type: name;
        var scope: array<CGameplayEntity>;
        var changed: bool;

        var vectorZero: Vector = Vector(0, 0, 0);

        if (!target || !target.lrdebugOneliner) return false;

        GetEditScope(target, scope);
        history.StartEdit(scope, "reset offset");

        rewriter = target.LRDebug_GetOrCreateRewriter();
        params = target.LRDebug_GetParams(rewriter);
        type = GetSelectedLightType(target);

        if (type != 'spot' && LRDebug_IsCandle(target)) {
            changed = params.pointLightOffset != vectorZero || !params.alignPointLights.has;

            params.pointLightOffset = vectorZero;
            params.alignPointLights.has = true;
            params.alignPointLights.value = true;
        }
        else {
            lightParams = GetActiveLightParams(params, target, type);
            changed = lightParams.offset.value != vectorZero || !lightParams.offset.has;

            lightParams.offset.has = true;
            lightParams.offset.value = vectorZero;
        }

        if (changed) ApplyParams(target, rewriter, params);
        history.Commit(changed);
        return changed;
    }

    private function ResetAdjustAccumulator() {
        adjustAccumulator = 0.0;
    }

    /**
     * Accumulates the analog mouse delta and releases it in whole `quantum` increments,
     * carrying the sub-quantum remainder so high-DPI / high-FPS movement isn't lost.
     */
    private function ConsumeQuantizedDelta(delta: float, quantum: float): float {
        var sign: float;
        var steps: float;

        adjustAccumulator += delta;
        steps = FloorF(AbsF(adjustAccumulator) / quantum);
        if (steps < 1.0) return 0.0;

        sign = SignF(adjustAccumulator);
        adjustAccumulator -= sign * steps * quantum;
        return sign * steps * quantum;
    }

    /** Minimum input step size. Colours are bytes. */
    private function GetAdjustQuantum(attr: name): float {
        switch (attr) {
            case 'colourR':
            case 'colourG':
            case 'colourB': return 1.0;
        }
        return 0.01;
    }

    /**
     * Per-attribute analog scale so a full mouse swipe covers each attribute's whole range
     * at the same "large swipe" feel as brightness (its 0-100 range is the reference).
     * alignOffsetZ stays 1.0: unbounded, left as-is for now.
     */
    private function GetAxisScale(attr: name): float {
        switch (attr) {
            case 'radius':             return 0.5;
            case 'attenuation':        return 0.01;
            case 'shadowBlendFactor':  return 0.01;
            case 'alignOffsetZ':       return 0.1;
            case 'softness':           return 0.02;
            case 'colourR':
            case 'colourG':
            case 'colourB':            return 2.55;
        }
        return 1.0;
    }

    /**
     * Flips a boolean attribute on the target and applies it live. Bools toggle on a
     * key-press rather than via analog hold-to-edit.
     */
    private function ToggleAttribute(target: CGameplayEntity, optional attr: name): bool {
        var light: CLightComponent;
        var params: CLightRewriteSourceParams;
        var lightParams: ILightRewriteParams;
        var rewriter: ILightSourceRewriter;
        var type: name;

        if (!target) return false;
        if (!target.lrdebugOneliner) return false;

        rewriter = target.LRDebug_GetOrCreateRewriter();
        params = target.LRDebug_GetParams(rewriter);

        type = GetSelectedLightType(target);
        if (attr == '') attr = GetCurrentAttrId(type);
        if (!IsAttrApplicable(attr, type)) return false;

        light = GetLight(target, type);

        switch (attr) {
            case 'useSpotlightColor':
                params.useSpotlightColor.has = true;
                params.useSpotlightColor.value = !params.useSpotlightColor.value;
                break;

            case 'alignPointLights':
                params.alignPointLights.has = true;
                params.alignPointLights.value = !params.alignPointLights.value;
                break;

            case 'overrideColour':
                lightParams = GetActiveLightParams(params, target, type);
                lightParams.color.has = !lightParams.color.has;
                if (lightParams.color.has && light) {
                    lightParams.color.value = light.color;
                }
                break;

            default:
                return false;
        }

        ApplyParams(target, rewriter, params);
        return true;
    }

    public function CycleShadowMode(target: CGameplayEntity) {
        var rewriter: ILightSourceRewriter;
        var params: CLightRewriteSourceParams;
        var type: name;
        var light: CLightComponent;
        var lightParams: ILightRewriteParams;
        var scope: array<CGameplayEntity>;

        if (!target) return;

        GetEditScope(target, scope);
        history.StartEdit(scope, "shadow mode");

        rewriter = target.LRDebug_GetOrCreateRewriter();
        params = target.LRDebug_GetParams(rewriter);
        type = GetSelectedLightType(target);
        light = GetLight(target, type);
        lightParams = GetActiveLightParams(params, target, type);

        if (!lightParams.castShadows.has) {
            lightParams.castShadows.has = true;
            if (light) lightParams.castShadows.value = light.shadowCastingMode;
        }
        lightParams.castShadows.value = NextShadowMode(lightParams.castShadows.value);

        ApplyParams(target, rewriter, params);
        history.Commit(true);
    }

    private function NextShadowMode(mode: ELightShadowCastingMode): ELightShadowCastingMode {
        switch (mode) {
            case LSCM_None:         return LSCM_OnlyDynamic;
            case LSCM_OnlyDynamic:  return LSCM_OnlyStatic;
            case LSCM_OnlyStatic:   return LSCM_Normal;
            case LSCM_Normal:       return LSCM_None;
        }
        return LSCM_None;
    }

    public function ToggleGroupEdit(): bool {
        groupEdit = !groupEdit;
        return groupEdit;
    }

    public function IsGroupEditing(): bool {
        return groupEdit;
    }

    /** Group size is the target plus every matched member (CacheGroupMembers excludes the target) */
    public function GetGroupMemberCount(target: CGameplayEntity): int {
        if (!target) return 0;

        CacheGroupMembers(target);
        return groupMembers.Size() + 1;
    }

    /** Every group member except the target, for marking their positions */
    public function GetGroupMembers(target: CGameplayEntity, out members: array<CGameplayEntity>) {
        members.Clear();
        if (!target) return;

        CacheGroupMembers(target);
        members = groupMembers;
    }

    public function Toggle(target: CGameplayEntity): bool {
        var changed: bool;
        var scope: array<CGameplayEntity>;

        if (!target) return false;

        GetEditScope(target, scope);
        history.StartEdit(scope, GetCurrentAttrLabel(GetSelectedLightType(target)));
        changed = ToggleAttribute(target);
        history.Commit(changed);
        return changed;
    }

    /** Analog edits span a key hold, so the snapshot is taken here and committed at EndAdjust */
    public function BeginAdjust(target: CGameplayEntity) {
        var scope: array<CGameplayEntity>;

        if (!target) return;

        ResetAdjustAccumulator();
        adjustChanged = false;
        GetEditScope(target, scope);
        history.StartEdit(scope, GetCurrentAttrLabel(GetSelectedLightType(target)));
    }

    public function EndAdjust() {
        history.Commit(adjustChanged);
    }

    /** The undo scope is every light the edit touches, so a group edit reverts all members */
    private function GetEditScope(target: CGameplayEntity, out entities: array<CGameplayEntity>) {
        var i, count: int;

        entities.Clear();
        if (!target) return;

        entities.PushBack(target);
        if (!groupEdit) return;

        CacheGroupMembers(target);
        count = groupMembers.Size();
        for (i = 0; i < count; i += 1) {
            if (groupMembers[i]) entities.PushBack(groupMembers[i]);
        }
    }

    private function ApplyParams(
        target: CGameplayEntity,
        rewriter: ILightSourceRewriter,
        params: CLightRewriteSourceParams
    ) {
        rewriter.LRDebug_SetMenuOverrideParams(params);
        rewriter.RestoreOriginalState();
        rewriter.RewriteLight();

        if (groupEdit) ApplyToGroup(target, params);
    }

    private function ApplyToGroup(target: CGameplayEntity, params: CLightRewriteSourceParams) {
        var entity: CGameplayEntity;
        var rewriter: ILightSourceRewriter;
        var memberParams: CLightRewriteSourceParams;
        var i, count: int;

        CacheGroupMembers(target);

        count = groupMembers.Size();
        for (i = 0; i < count; i += 1) {
            entity = groupMembers[i];
            if (!entity) continue;

            rewriter = entity.LRDebug_GetOrCreateRewriter();
            memberParams = entity.LRDebug_GetParams(rewriter);
            params.ApplyTo(memberParams);
            rewriter.LRDebug_SetMenuOverrideParams(memberParams);
            rewriter.RestoreOriginalState();
            rewriter.RewriteLight();
        }
    }

    private function CacheGroupMembers(target: CGameplayEntity) {
        var match: CLightRewriteMatchAll;
        var entities: array<CEntity>;
        var entity: CGameplayEntity;
        var i, count: int;

        if (groupEditTarget == target) return;

        groupEditTarget = target;
        groupMembers.Clear();

        match = BuildGroupMatch(target);
        theGame.GetEntitiesByTag(theGame.lightRewrite.TAG_HAS_LIGHT, entities);

        count = entities.Size();
        for (i = 0; i < count; i += 1) {
            entity = (CGameplayEntity)entities[i];
            if (!entity || entity == target) continue;
            if (!match.Matches(entity)) continue;
            groupMembers.PushBack(entity);
        }
    }

    private function BuildGroupMatch(target: CGameplayEntity): CLightRewriteMatchAll {
        var match: CLightRewriteMatchAll = new CLightRewriteMatchAll in this;
        var entityRule, layerRule: CLightRewriteMatchRule;

        entityRule = new CLightRewriteMatchRule in match;
        entityRule.matchType = LR_Match_Entity;
        entityRule.matchMode = LR_Match_Exact;
        entityRule.matchValue = entityRule.GetSubject(target);
        match.rules.PushBack(entityRule);

        layerRule = new CLightRewriteMatchRule in match;
        layerRule.matchType = LR_Match_Layer;
        layerRule.matchMode = LR_Match_Exact;
        layerRule.matchValue = layerRule.GetSubject(target);
        match.rules.PushBack(layerRule);

        return match;
    }
}
