/**
 * Light Rewrite's in-game light authoring overlay.
 *
 * Wires input actions to the label manager and attribute editor.
 * All domain logic lives in the dedicated files in this folder.
 *
 * Example input.settings (under [LRDebug]):
 *
 * IK_NumPad7=(Action=LRDebug_ToggleLabels)
 * IK_NumPad8=(Action=LRDebug_ToggleLabelPaths)
 * IK_NumPad9=(Action=LRDebug_GroupEdit)
 * IK_NumPeriod=(Action=LRDebug_TogglePositionExport)
 * IK_NumPad6=(Action=LRDebug_Lock)
 * IK_NumPad4=(Action=LRDebug_ResetLight)
 * IK_NumPad5=(Action=LRDebug_SolveSpacing)
 * IK_NumStar=(Action=LRDebug_ResetOffset)
 * IK_NumPad0=(Action=LRDebug_Undo)
 * IK_NumPad1=(Action=LRDebug_CycleLightDown)
 * IK_NumPad2=(Action=LRDebug_ExportEdited)
 * IK_NumPad3=(Action=LRDebug_CycleLightUp)
 * IK_Q=(Action=LRDebug_BrightnessModifier)
 * IK_1=(Action=LRDebug_RadiusModifier)
 * IK_5=(Action=LRDebug_SoftnessModifier)
 *
 * Hold-to-edit reads the engine's mouse-Y axis (GI_MouseDampY) directly, so it needs
 * no extra context or binding: holding a modifier locks the camera and feeds mouse-Y
 * into the selected attribute.
 *
 * In spot mode the point-only modifier keys are reused: UseSpotlightColor edits inner
 * angle, AlignPointLights edits outer angle, AlignOffsetZ edits the spotlight's offset Z,
 * and the dedicated SoftnessModifier edits softness.
 */

@addField(CR4Player) public var lrDebugLabels: bool;
@addField(CR4Player) public var lrDebugLabelManager: LRDebug_LabelManager;
@addField(CR4Player) public var lrDebugTargeting: LRDebug_Targeting;
@addField(CR4Player) public var lrDebugAttrEditor: LRDebug_AttributeEditor;
@addField(CR4Player) public var lrDebugHistory: LRDebug_EditHistory;
@addField(CR4Player) public var lrDebugTargetMarkers: LRDebug_TargetMarkers;
@addField(CR4Player) public var lrDebugGroupMarkers: LRDebug_GroupMarkers;
@addField(CR4Player) public var lrDebugUnknownMarkers: LRDebug_UnknownLightMarkers;
@addField(CR4Player) public var lrDebugUnalteredMarkers: LRDebug_UnalteredMarkers;
@addField(CR4Player) public var lrDebugAdjusting: bool;
@addField(CR4Player) public var lrDebugClock: LRDebug_Clock;

/*
 * Lifecycle
 */

@wrapMethod(CR4Player)
function OnSpawned(spawnData: SEntitySpawnData) {
    wrappedMethod(spawnData);

    AddTimer('LRDebug_DeferredLabelInstall', 1.f, false);
}

@addMethod(CR4Player)
timer function LRDebug_DeferredLabelInstall(dt: float, id: int) {
    if (!theGame || !thePlayer) return;

    theInput.LRDebug_EnsureInit();

    lrDebugLabelManager = new LRDebug_LabelManager in this;
    lrDebugLabelManager.Init();
    lrDebugTargeting = new LRDebug_Targeting in this;
    lrDebugAttrEditor = new LRDebug_AttributeEditor in this;
    lrDebugHistory = new LRDebug_EditHistory in this;
    lrDebugAttrEditor.SetHistory(lrDebugHistory);
    lrDebugTargetMarkers = new LRDebug_TargetMarkers in this;
    lrDebugTargetMarkers.Init();
    lrDebugGroupMarkers = new LRDebug_GroupMarkers in this;
    lrDebugGroupMarkers.Init();
    lrDebugUnknownMarkers = new LRDebug_UnknownLightMarkers in this;
    lrDebugUnknownMarkers.Init();
    lrDebugUnalteredMarkers = new LRDebug_UnalteredMarkers in this;
    lrDebugUnalteredMarkers.Init();
    lrDebugClock = new LRDebug_Clock in this;
    lrDebugClock.RegisterListeners();

    theInput.RegisterListener(this, 'LRDebug_OnInputToggleLabels', 'LRDebug_ToggleLabels');
    theInput.RegisterListener(this, 'LRDebug_OnInputToggleLabelPaths', 'LRDebug_ToggleLabelPaths');
    theInput.RegisterListener(
        this,
        'LRDebug_OnInputToggleUnalteredMarkers',
        'LRDebug_ToggleUnalteredMarkers'
    );
    theInput.RegisterListener(this, 'LRDebug_OnInputLock', 'LRDebug_Lock');
    theInput.RegisterListener(this, 'LRDebug_OnInputCycleLight', 'LRDebug_CycleLight');
    theInput.RegisterListener(this, 'LRDebug_OnInputCycleLightUp', 'LRDebug_CycleLightUp');
    theInput.RegisterListener(this, 'LRDebug_OnInputCycleLightDown', 'LRDebug_CycleLightDown');
    theInput.RegisterListener(this, 'LRDebug_OnInputToggleGroupEdit', 'LRDebug_GroupEdit');
    theInput.RegisterListener(this, 'LRDebug_OnInputTogglePositionExport', 'LRDebug_TogglePositionExport');
    theInput.RegisterListener(this, 'LRDebug_OnInputToggleRewriter', 'LRDebug_ToggleRewriter');
    theInput.RegisterListener(this, 'LRDebug_OnInputCycleShadowMode', 'LRDebug_CycleShadowMode');
    theInput.RegisterListener(this, 'LRDebug_OnInputExportEdited', 'LRDebug_ExportEdited');
    theInput.RegisterListener(this, 'LRDebug_OnInputResetLight', 'LRDebug_ResetLight');
    theInput.RegisterListener(this, 'LRDebug_OnInputResetOffset', 'LRDebug_ResetOffset');
    theInput.RegisterListener(this, 'LRDebug_OnInputSolveSpacing', 'LRDebug_SolveSpacing');
    theInput.RegisterListener(this, 'LRDebug_OnInputUndo', 'LRDebug_Undo');
    theInput.RegisterListener(this, 'LRDebug_OnBrightnessModifier', 'LRDebug_BrightnessModifier');
    theInput.RegisterListener(this, 'LRDebug_OnRadiusModifier', 'LRDebug_RadiusModifier');
    theInput.RegisterListener(this, 'LRDebug_OnAttenuationModifier', 'LRDebug_AttenuationModifier');
    theInput.RegisterListener(
        this,
        'LRDebug_OnShadowFadeDistanceModifier',
        'LRDebug_ShadowFadeDistanceModifier'
    );
    theInput.RegisterListener(
        this,
        'LRDebug_OnShadowFadeRangeModifier',
        'LRDebug_ShadowFadeRangeModifier'
    );
    theInput.RegisterListener(
        this,
        'LRDebug_OnShadowBlendFactorModifier',
        'LRDebug_ShadowBlendFactorModifier'
    );
    theInput.RegisterListener(
        this,
        'LRDebug_OnUseSpotlightColorModifier',
        'LRDebug_UseSpotlightColorModifier'
    );
    theInput.RegisterListener(
        this,
        'LRDebug_OnAlignPointLightsModifier',
        'LRDebug_AlignPointLightsModifier'
    );
    theInput.RegisterListener(
        this,
        'LRDebug_OnAlignOffsetZModifier',
        'LRDebug_AlignOffsetZModifier'
    );
    theInput.RegisterListener(
        this,
        'LRDebug_OnOverrideColourModifier',
        'LRDebug_OverrideColourModifier'
    );
    theInput.RegisterListener(this, 'LRDebug_OnColourRModifier', 'LRDebug_ColourRModifier');
    theInput.RegisterListener(this, 'LRDebug_OnColourGModifier', 'LRDebug_ColourGModifier');
    theInput.RegisterListener(this, 'LRDebug_OnColourBModifier', 'LRDebug_ColourBModifier');
    theInput.RegisterListener(this, 'LRDebug_OnSoftnessModifier', 'LRDebug_SoftnessModifier');

    theInput.RegisterListener(this, 'LRDebug_OnAltPressed', 'ShowDeveloperModeAlt');
}

/** Update labels when the mod is toggle on/off */
@wrapMethod(CLightRewriteSettings)
function OptionValueChanged(groupId: int, optionName: name, optionValue: string) {
    var wasEnabled: bool = isEnabled;

    wrappedMethod(groupId, optionName, optionValue);

    if (
        isEnabled != wasEnabled &&
        thePlayer &&
        thePlayer.lrDebugLabels &&
        thePlayer.lrDebugLabelManager
    ) {
        thePlayer.lrDebugLabelManager.RefreshTargetOneliner();
    }
}

/** Track lights that were edited */
@wrapMethod(CLightRewriteSettings)
function GetAllLightSourceTags(): array<name> {
    var tags: array<name>;

    tags = wrappedMethod();
    tags.PushBack('LR_DebugLight');
    return tags;
}

@addMethod(CR4Player)
timer function LRDebug_RefreshOnelinersTimer(dt: float, id: int) {
    if (!lrDebugLabels || !theGame || !thePlayer) return;

    lrDebugUnknownMarkers.Scan();
    // Before the lock check: a locked target is exactly when the mod toggle gets flicked
    lrDebugLabelManager.RefreshStatusLabels();

    if (
        lrDebugTargeting.IsLocked() ||
        theInput.IsActionPressed('LRDebug_CtrlModifier')
    ) {
        return;
    }

    lrDebugLabelManager.Update(lrDebugTargeting);
}

/*
 * Input handlers
 */

@addMethod(CR4Player)
public function LRDebug_OnInputToggleLabels(action: SInputAction): bool {
    if (!theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    lrDebugLabels = !lrDebugLabels;
    LogChannel('LRDebug', "LRDebug_Toggle: " + lrDebugLabels);

    RemoveTimer('LRDebug_RefreshOnelinersTimer');
    if (lrDebugLabels) {
        theInput.StoreContext('LRDebug');
        AddTimer('LRDebug_RefreshOnelinersTimer', 0.1f, true);

        if (lrDebugAttrEditor.IsGroupEditing()) {
            lrDebugLabelManager.ShowGroupLabel();
        }
        else {
            lrDebugLabelManager.HideGroupLabel();
        }

        lrDebugUnalteredMarkers.Start();
        lrDebugClock.Enable();
    }
    else {
        theInput.RestoreContext('LRDebug', true);
        lrDebugLabelManager.HideScreenLabels();
        lrDebugTargetMarkers.Hide();
        lrDebugGroupMarkers.Hide();
        lrDebugUnknownMarkers.Hide();
        lrDebugUnalteredMarkers.Stop();
        lrDebugClock.Disable();
    }

    return true;
}

@wrapMethod(CExplorationStateManager)
function PostStateChange() {
    wrappedMethod();

    if (thePlayer.lrDebugLabels && theInput.GetContext() == thePlayer.GetExplorationInputContext()) {
        theInput.StoreContext('LRDebug');
    }
}

@addMethod(CR4Player)
public function LRDebug_OnInputLock(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    lrDebugTargeting.ToggleLock();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnAltPressed(action: SInputAction): bool {
    if (
        lrDebugLabels &&
        (IsPressed(action) || IsReleased(action))
    ) {
        lrDebugLabelManager.RegenerateNearbyOneliners();
    }

    return false;
}

@addMethod(CR4Player)
public function LRDebug_OnInputToggleLabelPaths(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    lrDebugLabelManager.TogglePathLabels();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputToggleUnalteredMarkers(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    lrDebugUnalteredMarkers.Toggle();
    return true;
}

/*
 * Input: Attributes
 */

@addMethod(CR4Player)
public function LRDebug_OnInputCycleLight(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    lrDebugAttrEditor.SwapLightSelection(lrDebugTargeting.GetTarget());
    lrDebugLabelManager.RefreshTargetOneliner();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputCycleLightUp(action: SInputAction): bool {
    return LRDebug_CycleActiveLight(action, 1);
}

@addMethod(CR4Player)
public function LRDebug_OnInputCycleLightDown(action: SInputAction): bool {
    return LRDebug_CycleActiveLight(action, -1);
}

@addMethod(CR4Player)
public function LRDebug_CycleActiveLight(action: SInputAction, delta: int): bool {
    if (!lrDebugLabels || !IsPressed(action) || !thePlayer) return false;

    if (lrDebugAttrEditor.CycleActiveLight(lrDebugTargeting.GetTarget(), delta)) {
        lrDebugLabelManager.RefreshTargetOneliner();
        lrDebugLabelManager.RefreshPathLabel();
    }
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputToggleGroupEdit(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    if (lrDebugAttrEditor.ToggleGroupEdit()) lrDebugLabelManager.ShowGroupLabel();
    else lrDebugLabelManager.HideGroupLabel();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputTogglePositionExport(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    if (lrDebugAttrEditor.ToggleExportPositions()) {
        lrDebugLabelManager.HideGroupLabel();
        lrDebugLabelManager.ShowToast("Export: per light (with positions) - group edit off", 2.0);
    }
    else {
        lrDebugLabelManager.ShowGroupLabel();
        lrDebugLabelManager.ShowToast("Export: per template + layer (no positions) - group edit on", 2.0);
    }
    lrDebugLabelManager.RefreshStatusLabels();
    return true;
}

@addField(CInputManager) public var lr: LRDebug_Input;

@addMethod(CInputManager)
function LRDebug_EnsureInit() {
    if (!lr) {
        lr = new LRDebug_Input in this;
        lr.Init();
    }
}

@wrapMethod(CR4IngameMenu)
function OnConfigUI() {
    wrappedMethod();

    theInput.LRDebug_EnsureInit();
}

@addMethod(CR4Player)
public function LRDebug_OnInputToggleRewriter(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    lrDebugLabelManager.ToggleRewriterOnTarget();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputCycleShadowMode(action: SInputAction): bool {
    var target: CGameplayEntity;

    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    target = lrDebugTargeting.GetTarget();
    if (!target || !target.lrdebugOneliner) return true;

    lrDebugAttrEditor.CycleShadowMode(target);
    lrDebugLabelManager.RefreshTargetOneliner();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputExportEdited(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    LRDebug_ExportEditedLights();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputResetLight(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    lrDebugLabelManager.ResetTarget();
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputResetOffset(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    if (lrDebugAttrEditor.ResetOffset(lrDebugTargeting.GetTarget())) {
        lrDebugLabelManager.RefreshTargetOneliner();
    }
    return true;
}

/** Ignored mid-hold so an in-progress edit finishes before its predecessor is reverted */
@addMethod(CR4Player)
public function LRDebug_OnInputUndo(action: SInputAction): bool {
    if (
        !lrDebugLabels ||
        !IsPressed(action) ||
        !thePlayer ||
        lrDebugAdjusting ||
        !theInput.IsActionPressed('LRDebug_CtrlModifier')
    ) {
        return false;
    }

    lrDebugLabelManager.Undo(lrDebugHistory);
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnInputSolveSpacing(action: SInputAction): bool {
    if (!lrDebugLabels || !theInput.lr.IsNormalKeydown(action) || !thePlayer) return false;

    theGame.lightRewrite.ApplySpacing();
    LogChannel('LRDebug', "LRDebug spacing: re-spaced all lights");
    return true;
}

/*
 * Analogue input handlers
 */

@addMethod(CR4Player)
public function LRDebug_OnBrightnessModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 0);
}

@addMethod(CR4Player)
public function LRDebug_OnRadiusModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 1);
}

@addMethod(CR4Player)
public function LRDebug_OnAttenuationModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 2);
}

@addMethod(CR4Player)
public function LRDebug_OnShadowFadeDistanceModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 3);
}

@addMethod(CR4Player)
public function LRDebug_OnShadowFadeRangeModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 4);
}

@addMethod(CR4Player)
public function LRDebug_OnShadowBlendFactorModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 5);
}

/** Spot mode hold-edits inner angle; point mode toggles the bool */
@addMethod(CR4Player)
public function LRDebug_OnUseSpotlightColorModifier(action: SInputAction): bool {
    if (lrDebugAttrEditor.GetSelectedLightType(lrDebugTargeting.GetTarget()) == 'spot') {
        return LRDebug_EnterAdjust(action, 6);
    }
    return LRDebug_ToggleAttr(action, 6);
}

/** Spot mode hold-edits outer angle; point mode toggles the bool */
@addMethod(CR4Player)
public function LRDebug_OnAlignPointLightsModifier(action: SInputAction): bool {
    if (lrDebugAttrEditor.GetSelectedLightType(lrDebugTargeting.GetTarget()) == 'spot') {
        return LRDebug_EnterAdjust(action, 7);
    }
    return LRDebug_ToggleAttr(action, 7);
}

@addMethod(CR4Player)
public function LRDebug_OnAlignOffsetZModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 8);
}

@addMethod(CR4Player)
public function LRDebug_OnSoftnessModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 13);
}

@addMethod(CR4Player)
public function LRDebug_OnOverrideColourModifier(action: SInputAction): bool {
    return LRDebug_ToggleAttr(action, 9);
}

@addMethod(CR4Player)
public function LRDebug_OnColourRModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 10);
}

@addMethod(CR4Player)
public function LRDebug_OnColourGModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 11);
}

@addMethod(CR4Player)
public function LRDebug_OnColourBModifier(action: SInputAction): bool {
    return LRDebug_EnterAdjust(action, 12);
}

/**
 * On key-press, lock the camera (rotation stops but GI_MouseDamp values keep flowing)
 * and flag adjust mode so the mouse-Y listener edits the chosen attribute live; the
 * matching unlock happens on release.
 */
@addMethod(CR4Player)
public function LRDebug_EnterAdjust(action: SInputAction, attrIndex: int): bool {
    if (!lrDebugLabels || !thePlayer) return false;

    if (IsPressed(action)) {
        theInput.lr.CaptureMouseMovement(this, 'LRDebug_OnMouseAxisX', 'LRDebug_OnMouseAxisY');

        lrDebugAttrEditor.SetAttributeIndex(attrIndex);
        lrDebugAttrEditor.BeginAdjust(lrDebugTargeting.GetTarget());
        lrDebugLabelManager.RefreshTargetOneliner();
        lrDebugAdjusting = true;
        return true;
    }

    if (IsReleased(action)) {
        theInput.lr.ReleaseMouseMovement(this);

        lrDebugAdjusting = false;
        lrDebugAttrEditor.EndAdjust();
        return true;
    }

    return false;
}

@addMethod(CR4Player)
public function LRDebug_ToggleAttr(action: SInputAction, attrIndex: int): bool {
    if (!lrDebugLabels || !IsPressed(action) || !thePlayer) return false;

    lrDebugAttrEditor.SetAttributeIndex(attrIndex);
    if (lrDebugAttrEditor.Toggle(lrDebugTargeting.GetTarget())) {
        lrDebugLabelManager.RefreshTargetOneliner();
    }
    return true;
}

/** Holding Alt while editing the offset turns vertical mouse movement into XY dragging instead of Z adjustment */
@addMethod(CR4Player)
public function LRDebug_MovingOffsetXY(): bool {
    return lrDebugAttrEditor.IsEditingOffset()
        && theInput.IsActionPressed('ShowDeveloperModeAlt');
}

@addMethod(CR4Player)
public function LRDebug_OnMouseAxisX(action: SInputAction): bool {
    var modifier: float = 1.0;

    if (!lrDebugAdjusting || action.value == 0.0 || !thePlayer) return false;
    if (!LRDebug_MovingOffsetXY()) return false;

    if (theInput.IsActionPressed('LRDebug_CtrlModifier')) {
        modifier = 0.2;
    }

    if (lrDebugAttrEditor.MoveOffsetXY(action.value * theInput.lr.ADJUST_AXIS_SENSITIVITY * modifier, 0.0, lrDebugTargeting.GetTarget())) {
        lrDebugLabelManager.RefreshTargetOneliner();
    }
    return true;
}

@addMethod(CR4Player)
public function LRDebug_OnMouseAxisY(action: SInputAction): bool {
    var changeMade: bool;
    var modifier: float = 1.0;

    if (!lrDebugAdjusting || action.value == 0.0 || !thePlayer) return false;

    if (theInput.IsActionPressed('LRDebug_CtrlModifier')) {
        modifier = 0.2;
    }

    if (LRDebug_MovingOffsetXY()) {
        changeMade = lrDebugAttrEditor.MoveOffsetXY(
            0.0,
            -action.value * theInput.lr.ADJUST_AXIS_SENSITIVITY * modifier,
            lrDebugTargeting.GetTarget()
        );

        if (changeMade) lrDebugLabelManager.RefreshTargetOneliner();
        return true;
    }

    changeMade = lrDebugAttrEditor.AdjustAttributeContinuous(
        -action.value * theInput.lr.ADJUST_AXIS_SENSITIVITY * modifier,
        lrDebugTargeting.GetTarget()
    );

    if (changeMade) lrDebugLabelManager.RefreshTargetOneliner();
    return true;
}
